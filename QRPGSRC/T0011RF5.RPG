      **************************************************************
      *  PROGRAM         T0011RF5
      *  APPLICATION     UIM/RPG SAMPLE
      *  PURPOSE         This fills the popup selection lists.
      *  NOTES
      *    This program is called by T0011RP6 to fill the popup
      *    selection lists.
      **************************************************************
      *   Include the UIM constants, and declare the VARRCD
      **************************************************************
     I/COPY QUSRTOOL/QATTRPG,T0011RI6
      **************************************************************
      *   Declare varrcd for selection list processing
      **************************************************************
     IVARRCD     IDS
     I                                    B   1   20OPT1
     I                                        3   4 STATE
     I                                        5  29 STNAME
      *   Size of Varrcd
     I              29                    C         VLEN
      **************************************************************
      *   Declare and copy the entry parameters.
      **************************************************************
     C           *ENTRY    PLIST
     C                     PARM           APPHN#  8
     C                     PARM           LSTNA# 10
     C                     MOVE APPHN#    APPHND
     C                     MOVE LSTNA#    LSTNAM
      **************************************************************
      *   Test the list name.
      **************************************************************
     C                     SELEC
     C           LSTNAM    WHEQ 'STALIST'
     C                     EXSR STAFIL
     C                     ENDSL
     C                     MOVE *ON       *INLR
     C                     RETRN
      **************************************************************
      *   Subroutine to fill the state abbreviations list
      **************************************************************
     C           STAFIL    BEGSR
     C                     MOVEL'SELVAR'  VARNAM    P
     C                     Z-ADDVLEN      VARLEN
     C                     CALL 'QUIGETV' GETV
      *   Save the STATE so we can restore it later
     C                     MOVE STATE     STATE2  2
      *   Add one dummy (ensure the list exists) and delete the list
     C                     CALL 'QUIADDLE'ADDLE
     C                     CALL 'QUIDLTL' DLTL
      **************************************************************
      *   Add several states to the list.  In actuallity one should
      *   read these names from a datafile or message file, but this
      *   is an example.
      **************************************************************
     C                     Z-ADD0         OPT1
     C                     MOVEL'Florida' STNAME    P
     C                     MOVE 'FL'      STATE
     C                     CALL 'QUIADDLE'ADDLE
     C                     MOVEL'Texas'   STNAME    P
     C                     MOVE 'TX'      STATE
     C                     CALL 'QUIADDLE'ADDLE
     C                     MOVEL'Oregon'  STNAME    P
     C                     MOVE 'OR'      STATE
     C                     CALL 'QUIADDLE'ADDLE
     C                     MOVEL'Virginia'STNAME    P
     C                     MOVE 'VA'      STATE
     C                     CALL 'QUIADDLE'ADDLE
     C                     MOVEL'Ohio'    STNAME    P
     C                     MOVE 'OH'      STATE
     C                     CALL 'QUIADDLE'ADDLE
     C                     MOVEL'Alabama' STNAME    P
     C                     MOVE 'AL'      STATE
     C                     CALL 'QUIADDLE'ADDLE
     C                     MOVEL'Kentucky'STNAME    P
     C                     MOVE 'KY'      STATE
     C                     CALL 'QUIADDLE'ADDLE
     C                     MOVEL'New York'STNAME    P
     C                     MOVE 'NY'      STATE
     C                     CALL 'QUIADDLE'ADDLE
      *   Restore the previous value of STATE
     C                     MOVE STATE2    STATE
     C                     CALL 'QUIPUTV' PUTV
     C                     ENDSR
      **************************************************************
     I/COPY QUSRTOOL/QATTRPG,T0011RI5
